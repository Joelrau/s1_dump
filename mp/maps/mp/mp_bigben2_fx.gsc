#include common_scripts\Utility;

main()
{
	level._effect[ "mp_bigb_water_stream_med" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_water_stream_med" );
	level._effect[ "mp_bigb_falling_water_splash_runner" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_falling_water_splash_runner" );
	level._effect[ "dirty_foam_flowing" ] = loadfx( "vfx/map/mp_bigben/dirty_foam_flowing" );
	level._effect[ "mp_bigb_window_glow_03" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_window_glow_03" );
	level._effect[ "mp_bigb_window_glow_02" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_window_glow_02" );
	level._effect[ "mp_bigb_rain_splash_200x200_no_distort" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_rain_splash_200x200_no_distort" );
	level._effect[ "mp_bigb_window_glow" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_window_glow" );
	level._effect[ "rain_splash_sm_runner_no_ripple" ] = loadfx( "vfx/rain/rain_splash_sm_runner_no_ripple" );
	level._effect[ "mp_bigb_fire_vista_glow" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_fire_vista_glow" );
	level._effect[ "mp_bigb_killstreak_curvy_missile" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_killstreak_curvy_missile" );
	level._effect[ "mp_bigb_rain_splash_line" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_rain_splash_line" );
	level._effect[ "mp_bigb_firelight" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_firelight" );
	level._effect[ "mp_bigb_river_fire" ] = loadfx( "vfx/fire/mp_bigb_river_fire" );
	level._effect[ "mp_bigb_light_mist" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_light_mist" );
	level._effect[ "electrical_sparks_small_runner" ] = loadfx( "vfx/explosion/electrical_sparks_small_runner" );
	level._effect[ "jet_flyover" ] = loadfx( "vfx/unique/jet_flyover" );
	level._effect[ "dust_falling_int_runner" ] = loadfx( "vfx/dust/dust_falling_int_runner" );
	level._effect[ "mp_bigb_bridge_smoke" ] = loadfx( "vfx/map/mp_bigben/mp_bigb_bridge_smoke" );
	level._effect[ "bigb_light_vent_steam" ] = loadfx( "vfx/map/mp_bigben/bigb_light_vent_steam" );
	
	level._effect[ "mp_bigb_ambient_int_dust" ] 						= loadfx( "vfx/map/mp_bigben/mp_bigb_ambient_int_dust" );
	level._effect[ "battlefield_smoke_m_dark" ] 						= loadfx( "vfx/smoke/dlc/battlefield_smoke_m_dark" );
	level._effect[ "mp_bigb_deck_wind_runner" ] 						= loadfx( "vfx/map/mp_bigben/mp_bigb_deck_wind_runner" );
	level._effect[ "bigb_aa_flash_tracer" ] 							= loadfx( "vfx/map/mp_bigben/bigb_aa_flash_tracer" );
	level._effect[ "mp_bigb_searchlights" ] 							= loadfx( "vfx/map/mp_bigben/mp_bigb_searchlights" );
	level._effect[ "mp_bigb_building_hit" ] 							= loadfx( "vfx/map/mp_bigben/mp_bigb_building_hit" );
	level._effect[ "mp_bigb_boat_explosion" ] 							= loadfx( "vfx/map/mp_bigben/mp_bigb_boat_explosion" );
	level._effect[ "mp_bigb_atm_paper_runner" ] 						= loadfx( "vfx/map/mp_bigben/mp_bigb_atm_paper_runner" );
	level._effect[ "mp_bigb_water_edge_splash_sml_runner" ] 			= loadfx( "vfx/map/mp_bigben/mp_bigb_water_edge_splash_sml_runner" );
	level._effect[ "mp_trash_blowing_runner" ] 							= loadfx( "vfx/map/mp_lost/mp_trash_blowing_runner" );
	level._effect[ "mp_bigb_concrete_debris_water_runner" ] 			= loadfx( "vfx/map/mp_bigben/mp_bigb_concrete_debris_water_runner" );
	level._effect[ "mp_bigb_building_smoke_debris_runner" ] 			= loadfx( "vfx/map/mp_bigben/mp_bigb_building_smoke_debris_runner" );	
	level._effect[ "mp_bigb_water_explosion_runner" ] 					= loadfx( "vfx/map/mp_bigben/mp_bigb_water_explosion_runner" );
	level._effect[ "steam_pipe_burst_lp_xsml" ] 						= loadfx( "vfx/steam/dlc/steam_pipe_burst_lp_xsml" );
	level._effect[ "mp_bigb_falling_leaves_runner" ] 					= loadfx( "vfx/map/mp_bigben/mp_bigb_falling_leaves_runner" );
	level._effect[ "mp_bigb_bus_headlight_flicker" ] 					= loadfx( "vfx/map/mp_bigben/mp_bigb_bus_headlight_flicker" );
	level._effect[ "mp_bigb_bus_headlight" ] 							= loadfx( "vfx/map/mp_bigben/mp_bigb_bus_headlight" );
	level._effect[ "mp_bigb_vista_smk_stack" ] 							= loadfx( "vfx/map/mp_bigben/mp_bigb_vista_smk_stack" );
	level._effect[ "mp_bigb_water_mist_deck_lrg_runner" ] 				= loadfx( "vfx/map/mp_bigben/mp_bigb_water_mist_deck_lrg_runner" );
	level._effect[ "falling_water_drip_runner_slow" ] 					= loadfx( "vfx/water/falling_water_drip_runner_slow" );
	level._effect[ "smoke_pillar_white_01" ] 							= loadfx( "vfx/smoke/smoke_pillar_white_01" );
	level._effect[ "water_stream_med" ]	 								= loadfx( "vfx/rain/water_stream_med" );
	level._effect[ "mp_bigb_falling_water_exterior_short" ] 			= loadfx( "vfx/map/mp_bigben/mp_bigb_falling_water_exterior_short" );
	level._effect[ "mp_bigb_water_edge_runoff_sml" ] 					= loadfx( "vfx/map/mp_bigben/mp_bigb_water_edge_runoff_sml" );
	level._effect[ "mp_bigb_water_edge_runoff" ] 						= loadfx( "vfx/map/mp_bigben/mp_bigb_water_edge_runoff" );
	level._effect[ "mp_bigb_falling_water_exterior" ] 					= loadfx( "vfx/map/mp_bigben/mp_bigb_falling_water_exterior" );
	level._effect[ "mp_bigb_rain_splash_sm_50x50_lp" ] 					= loadfx( "vfx/map/mp_bigben/mp_bigb_rain_splash_sm_50x50_lp" );
	level._effect[ "rain_splat_on_lens_sml_rnr_night" ] 				= loadfx( "vfx/rain/rain_splat_on_lens_sml_rnr_night" );
	level._effect[ "mp_bigb_water_explosion" ] 							= loadfx( "vfx/map/mp_bigben/mp_bigb_water_explosion" );
	level._effect[ "mp_bigb_rain_volume_far" ] 							= loadfx( "vfx/map/mp_bigben/mp_bigb_rain_volume_far" );
	level._effect[ "mp_bigb_fire_l_blacksmk_nonlit" ] 					= loadfx( "vfx/map/mp_bigben/mp_bigb_fire_l_blacksmk_nonlit" );
	level._effect[ "mp_bigb_fire_lp_clocktower_smoke" ] 				= loadfx( "vfx/map/mp_bigben/mp_bigb_fire_lp_clocktower_smoke" );
	level._effect[ "smoke_pillar_black_medium_slow" ] 					= loadfx( "vfx/smoke/smoke_pillar_black_medium_slow" );
	level._effect[ "mp_bigb_building_explosion_runner" ]				= loadfx( "vfx/map/mp_bigben/mp_bigb_building_explosion_runner" );
	level._effect[ "fire_tiny_area" ] 									= loadfx( "vfx/map/mp_blackbox/fire_tiny_area" );
	level._effect[ "fire_falling_runner_point_infrequent" ] 			= loadfx( "vfx/fire/fire_falling_runner_point_infrequent" );
	level._effect[ "fire_vista_lp_lrg_blacksmk_thick" ] 				= loadfx( "vfx/fire/fire_vista_lp_lrg_blacksmk_thick" );
	level._effect[ "electrical_sparks_runner_slit" ] 					= loadfx( "vfx/explosion/electrical_sparks_runner_slit" );
	level._effect[ "fire_lp_m_no_light" ] 								= loadfx( "vfx/fire/fire_lp_m_no_light" );
	level._effect[ "fire_lp_s_no_light" ] 								= loadfx( "vfx/fire/fire_lp_s_no_light" );
	level._effect[ "mp_bigb_rain_splash_sm_200x200_lp" ] 				= loadfx( "vfx/map/mp_bigben/mp_bigb_rain_splash_sm_200x200_lp" );
	level._effect[ "mp_bigb_rain_splash_sm_100x100_lp" ] 				= loadfx( "vfx/map/mp_bigben/mp_bigb_rain_splash_sm_100x100_lp" );
	level._effect[ "mp_bigb_rain_volume_close" ] 						= loadfx( "vfx/map/mp_bigben/mp_bigb_rain_volume_close" );
	level._effect[ "mp_bigb_water_displacement_patch" ] 				= loadfx( "vfx/map/mp_bigben/mp_bigb_water_displacement_patch" );

// Killstreak vfx
	level._effect[ "smoketrail_rpg" ] 									= loadfx( "vfx/trail/smoketrail_rpg" );
	level._effect[ "smoketrail_missile_thick" ] 						= loadfx( "vfx/trail/smoketrail_missile_thick" );
	level._effect[ "mp_bigb_killstreak_rocket_explosion" ] 				= loadfx( "vfx/map/mp_bigben/mp_bigb_killstreak_rocket_explosion" );
	level._effect[ "mp_bigb_killstreak_ventsmoke" ] 					= loadfx( "vfx/map/mp_bigben/mp_bigb_killstreak_ventsmoke" );
	level._effect[ "mp_bigb_killstreak_rockettrail" ] 					= loadfx( "vfx/map/mp_bigben/mp_bigb_killstreak_rockettrail" );
	
/#
    if ( getdvar( "clientSideEffects" ) != "1" )
        maps\createfx\mp_bigben2_fx::main();
#/

	// Play Persistent VFX for all players connecting
	level thread onPlayerConnect();
	
	// Play the one-off vfx at appropriate server times
	level thread start_temporary_fx();
	
	// Ensure the 2 missile explosions occurring near the boat play consistently for all clients at the same time
	// Note: There's an earthquake and rumble effect that plays for these, so for gameplay purposes must play universally on all clients at the same time
	level thread manage_close_explosions();
}

onPlayerConnect()
{
	while( true )
	{
		level waittill( "connected", player );
		level thread start_persistent_fx( player );
	}
}

start_persistent_fx( player )
{
	if( !( IsDefined( level.skylight_vista_fx_off ) && level.skylight_vista_fx_off ) )
	{
		// Play the persistent VFX
		level thread rain_splashes( player );
		level thread vista_smoke( player );
		level thread water_explosions_far( player );
		level thread bridge_rubble( player );
	}
	
	// If the primary bridge explosion already occurred, make sure it plays for the newly connected player
	if( IsDefined( level.bridgeExploded ) && level.bridgeExploded )
	{
		// Play the VFX on just this client (presumably because they joined after the big bridge explosion)
		level thread bridge_explosion( player );
	}
}

start_temporary_fx()
{
	level thread boat_explosion_bow();
	level thread boat_explosion_stern();
	level thread car_explosion_bridge();
	level thread car_explosion();
	level thread bus_explosion();
	level thread rock_explosion_bridge();
	
	// Wait 5 Minutes before exploding the bridge
	wait 300;
	
	level.bridgeExploded = true;
	
	// Play the VFX on all clients (null parameter implies play on all clients)
	level thread bridge_explosion();
}

// Manually fire the VFX Missiles that shake a player's screen as we want them to be consistent across all players
manage_close_explosions()
{
	// Wait until players begin connecting to the game
	while( !IsDefined( level.players ) )
		wait 1;
	
	while( true )
	{
		// Wait a random few seconds
		rand = RandomIntRange( 8, 12 );
		wait rand;

		// Don't Fire While Skylight is in the air
		while( IsDefined( level.skylight_available ) && !level.skylight_available )
			wait 40;
		
		// Fire the Bow Explosions VFX Rocket!
		level thread water_explosions_bow();
		
		// Wait a random few seconds
		rand = RandomIntRange( 8, 12 );
		wait rand;
		
		// Don't Fire While Skylight is in the air
		while( IsDefined( level.skylight_available ) && !level.skylight_available )
			wait 40;
		
		// Fire the Bow Explosions VFX Rocket!
		level thread water_explosions_stern();
	}
}

water_explosions_bow()
{
	level thread common_scripts\_exploder::activate_clientside_exploder( 4 );
	
	// Wait for the vfx missile to fall
	wait 1;
	
	level thread water_explosion_earthquake_bow();
	
	wait 5;
	
	// Stop the effect
	StopClientExploder( 4 );
}

water_explosion_earthquake_bow()
{
	explosionRadius = 1000;
	explosionOrigin = ( 5345.45, 1850.61, 145 );

	Earthquake( .9, 1, explosionOrigin, explosionRadius );
	
	foreach( player in level.players )
	{
		if( !( IsDefined( player.team ) && player.team == "spectator" ) )
		{
			if( DistanceSquared( player.origin, explosionOrigin ) < ( explosionRadius*explosionRadius ) )
			{
				player PlayRumbleOnEntity("heavygun_fire"); 
			}
		}
	}
}

water_explosions_stern()
{
	level thread common_scripts\_exploder::activate_clientside_exploder( 3 );
	
	// Wait for the vfx missile to fall
	wait 1;
	
	level thread water_explosion_earthquake_stern();
	
	wait 5;
	
	// Stop the effect
	StopClientExploder( 3 );
}

water_explosion_earthquake_stern()
{
	explosionRadius = 1000;
	explosionOrigin = ( 2889.97, 2035.69, 147 );

	Earthquake( .9, 1, explosionOrigin, explosionRadius );
	
	foreach( player in level.players )
	{
		if( !( IsDefined( player.team ) && player.team == "spectator" ) )
		{
			if( DistanceSquared( player.origin, explosionOrigin ) < ( explosionRadius*explosionRadius ) )
			{
				player PlayRumbleOnEntity("heavygun_fire");
			}
		}
	}
}

water_explosions_far( player )
{
	level thread common_scripts\_exploder::activate_clientside_exploder( 5, player ); //explodernumnber,player
}

boat_explosion_bow()
{
	rand = RandomIntRange( 30, 550 );
	wait rand;
	
	start_fx_exploder();
	level thread common_scripts\_exploder::activate_clientside_exploder( 10 );
	finish_fx_exploder();
}

boat_explosion_stern()
{
	rand = RandomIntRange( 30, 550 );
	wait rand;
	
	start_fx_exploder();
	level thread common_scripts\_exploder::activate_clientside_exploder( 11 );
	finish_fx_exploder();
}

car_explosion()
{
	rand = RandomIntRange( 30, 550 );
	wait rand;
	
	start_fx_exploder();
	level thread common_scripts\_exploder::activate_clientside_exploder( 21 );
	finish_fx_exploder();
}

bus_explosion()
{
	rand=RandomIntRange( 30, 550 );
	wait rand;
	
	start_fx_exploder();
	level thread common_scripts\_exploder::activate_clientside_exploder( 22 );
	finish_fx_exploder();
}

// Bridge vfx
bridge_explosion( player )
{
	// Note: If player is null, then this VFX plays on all clients; otherwise it only plays on that player
	level thread common_scripts\_exploder::activate_clientside_exploder( 23, player );
	
	FireLgtEnts = GetScriptableArray( "fiyah", "targetname" );
	
	foreach( FireLgtEnt in FireLgtEnts )
	{
		FireLgtEnt SetScriptablePartState( 0, 1 );
	}
}

car_explosion_bridge()
{
	rand = RandomIntRange( 30, 550 );
	wait rand;
	
	start_fx_exploder();
	level thread common_scripts\_exploder::activate_clientside_exploder( 24 );
	finish_fx_exploder();
}

rock_explosion_bridge()
{
	rand = RandomIntRange( 30, 550 );
	wait rand;
	
	start_fx_exploder();
	level thread common_scripts\_exploder::activate_clientside_exploder( 25 );
	finish_fx_exploder();
}

bridge_rubble( player )
{
	level thread common_scripts\_exploder::activate_clientside_exploder( 31, player ); //explodernumnber,player
}

vista_smoke( player )
{
	level thread common_scripts\_exploder::activate_clientside_exploder( 32, player ); //explodernumnber,player
}

rain_splashes( player )
{
	level thread common_scripts\_exploder::activate_clientside_exploder( 33, player ); //explodernumnber,player
}

// Stop various VFX while Skylight is active
stop_vista_vfx()
{
	level endon( "skylight_start" );
	
	level.skylight_vista_fx_off = true;
	
	wait 0.1;
	
	level thread killstreak_exhaust_smoke();
	
	//IPrintLnBold ("Stop_missiles");
	
	wait 0.1;
	
	StopClientExploder( 5 ); // water_explosions_far
	StopClientExploder( 31 ); // bridge_rubble
	
	wait 0.1;
	
	StopClientExploder( 32 ); // vista_smoke
	StopClientExploder( 33 ); // rain_splashes
	
	// *** Skylight is actively firing ***
	wait 40;
	
	//IPrintLnBold ("Start_Vista");
	
	waittillframeend;
	
	foreach( player in level.players )
	{
		level thread restart_vista_fx( player );
	}
	
	level.skylight_vista_fx_off = false;
}

restart_vista_fx( player )
{
	level thread rain_splashes( player );
	
	wait 0.1;
	
	level thread vista_smoke( player );
	
	wait 0.1;
	
	level thread water_explosions_far( player );
	
	wait 0.1;
	
	level thread bridge_rubble( player );
}

killstreak_exhaust_smoke()
{
	level thread common_scripts\_exploder::activate_clientside_exploder( 30 );
	
	wait 2;
	
	//IPrintLnBold("stop_dat_steam");
	StopClientExploder( 30 );
}

// Attempt to play an fx...but first check if one is already playing
start_fx_exploder()
{
	while( IsDefined( level.exploderExploding ) && level.exploderExploding )
	{
		wait 7;
	}
	
	level.exploderExploding = true;
}

// After a few seconds, allow another exploder to play
finish_fx_exploder()
{
	wait 5;
	level.exploderExploding = false;
}